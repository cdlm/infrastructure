typeset -U path fpath manpath
export -TU INFOPATH infopath

path=(/usr/local/bin $path)

# homebrew
local bp='/opt/homebrew' #`brew --prefix`
if [ -d $bp ]; then
   path=(
      $bp/sbin
      $bp/bin
      $bp/share/python
      $path)
fi
unset bp

# standalone texlive, selected from ~/.texliveVersion
function texlive_set_paths() {
    local texliveVersion texlivePrefix
    texliveVersion=${1:-$(< ~/.texliveVersion)} 2>/dev/null
    texlivePrefix=/usr/local/texlive/${texliveVersion:-2010}

    path+=$texlivePrefix/bin/universal-darwin
    infopath+=$texlivePrefix/texmf/doc/info
    MANPATH+=$texlivePrefix/texmf/doc/man
}
texlive_set_paths

# haskell cabal
local hs="$HOME/.cabal"
if [ -d $hs ]; then
    path=($hs/bin $path)
    manpath=($hs/share/man $manpath)
fi
unset hs

# user-specific tools
path=($HOME/bin $path)

# Ruby with either rbenv or rvm
[[ -x "$HOME/.rbenv/bin/rbenv" ]] && path=($HOME/.rbenv/bin $path)
if command which -s rbenv; then
    eval "$(rbenv init -)"
elif [[ -s "$HOME/.rvm/scripts/rvm" ]]; then # fallback on rvm
    . "$HOME/.rvm/scripts/rvm"
    __rvm_project_rvmrc
fi
