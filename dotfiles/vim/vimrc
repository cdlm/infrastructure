set nocompatible
set encoding=utf-8
set fileencoding=utf-8

" Load pathogen bundles
"let g:solarized_visibility = 'low'
"let g:solarized_hitrail = 1
let g:pathogen_disabled = []
if !has('ruby') || !has('gui_running')
    call add(g:pathogen_disabled, 'TeX-9')
endif
call pathogen#runtime_append_all_bundles()
call pathogen#helptags()

" Load project-local settings if any
set exrc secure

" Don't start autocomplpop too eagerly
let g:acp_behaviorKeywordLength = 3

" Prefix/namespace for user commands
let mapleader=";"

" Anticipating some common typos
function! CommandAlias(abbreviation, expansion)
    execute 'cabbr ' . a:abbreviation . ' <c-r>=getcmdpos() == 1 && getcmdtype() == ":" ? "' . a:expansion . '" : "' . a:abbreviation . '"<CR>'
endfunction
command! -nargs=+ CommandAlias call CommandAlias(<f-args>)
" Use it on itself to define a simpler abbreviation for itself.
CommandAlias alias CommandAlias

" Quick access to .vimrc & testing vimscript code
nmap <leader>erc :edit $MYVIMRC<cr>
nmap <leader>src :source $MYVIMRC<cr>
function! DoIt() range
    let lines = join(getline(a:firstline, a:lastline), "\n")
    execute lines
endfunction
command! DoIt call DoIt()
map <leader>d :DoIt<cr>

" Remap 'quit' keys
nnoremap q :q
CommandAlias qw wq
" macros can still be nice sometimes, so put them on € (option-@)
nnoremap € q

syntax on
filetype plugin indent on
set omnifunc=syntaxcomplete#Complete

set laststatus=2
let &statusline=' %f%( %y%m%r%)%=L%l C%v '
function StatuslineGitBranch()
    if exists('*GitBranch')
        let result = GitBranch()
        if result != ''
            return '[' . result . ']'
        endif
    endif
    return ''
endfunction
set statusline+=%(%{StatuslineGitBranch()}\ %)
if version >= 700
    " set the status line to flashy colors in insert mode
    autocmd InsertEnter * highlight StatusLine term=reverse ctermfg=15 ctermbg=9
    autocmd InsertEnter * highlight ModeMsg    term=reverse ctermfg=15 ctermbg=9
    autocmd InsertLeave * highlight StatusLine term=reverse ctermfg=14 ctermbg=0 gui=bold,reverse
    autocmd InsertLeave * highlight ModeMsg    term=NONE    ctermfg=4  ctermbg=NONE
    " unfortunately impossible to reuse the variables from solarized.vim
    "autocmd InsertEnter * exe "highlight! StatusLine" . s:fmt_none . s:fg_base3 . :bg_orange . s:fmt_revbb
    "autocmd InsertLeave * exe "highlight! StatusLine" . s:fmt_none . s:fg_base1 . s:bg_base01 . s:fmt_revbb
endif

set ignorecase
set smartcase
set incsearch
set hlsearch
set showmatch
set wildmenu wildmode=list:longest,full wildignore+=*.swp,*.bak,*.pyc,*.elc,*.zwc,*.class,*.git
set ruler
set showcmd
set showmode
"set number
set hidden
set wrap
nmap <leader>w :set wrap!<cr>
set visualbell
set fillchars=""
set cursorline

" Integrate copy/paste with Mac OS
set clipboard=unnamed
"set mouse=a

" Sane indentation defaults
set tabstop=4
set shiftwidth=4
set softtabstop=4
set expandtab
set smartindent
set autoindent

set backspace=indent,eol,start
"set cpoptions+=$ " mark changed area
set whichwrap=b,s,h,l,<,>,[,]
" <Del> works, I don't see why <BS> shouldn't
map <BS> X

" Command-T binds to <leader>t and <leader>b
let g:CommandTMaxHeight=7
let g:CommandTMatchWindowReverse=1
nmap <leader>nt :NERDTreeToggle<cr>
nmap <leader>qf :botright copen<cr>
nmap <leader>spell :setlocal spell!<cr>

" Sudo write that file!
command SudoWrite write !sudo tee % > /dev/null
cmap w!! :SudoWrite

" Invisible characters: shortcut to rapidly toggle
nmap <leader>i :set list!<CR>
set listchars=tab:▸\ ,eol:¬,trail:·,nbsp:_,extends:→,precedes:→
"highlight NonText guifg=#4a4a59
"highlight SpecialKey guifg=#4a4a59

" Unhighlight search results in normal mode
nmap <silent> <leader>/ :nohlsearch<CR>

" Opening files relative to current one, e.g. :e %/bar.txt
cnoremap %% <C-r>=expand('%:p:.:h') . '/' <Enter>

" Remap line-exchange commands to match TextMate's shortcuts. Thanks to vimcasts.org for this :)
" Requires vim-unimpaired
nmap <C-Up> [e
nmap <C-Down> ]e
vmap <C-Up> [egv
vmap <C-Down> ]egv
vnoremap < <gv
vnoremap > >gv

" Emacs-style jump to end of line
imap <C-e> <C-o>A
imap <C-a> <C-o>I
" move around according to visual lines
nnoremap <up> gk
nnoremap <down> gj

" Open line above (ctrl-shift-o much easier than ctrl-o shift-O)
"imap <C-Enter> <C-o>o
"nmap <C-Enter> o
"imap <C-S-Enter> <C-o>O
"nmap <C-S-Enter> O

" Show highlighting group for current word
function! <SID>SyntaxStack()
    if !exists("*synstack")
        return
    endif
    echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunction
command SyntaxStack call <SID>SyntaxStack()
nmap <leader>P :SyntaxStack<Enter>

" Create directories when saving
augroup BWCCreateDir
    autocmd!
    autocmd BufWritePre * if expand("<afile>")!~#'^\w\+:/' && !isdirectory(expand("%:h")) | execute "silent! !mkdir -p %:h" | redraw! | endif
augroup END

" Remove GUI menu and toolbar
set guioptions-=T
set guifont=Consolas:h11,Menlo:h11
"set guioptions-=m
    " Mac-like tab navigation
map <D-S-]> gt
map <D-S-[> gT

set background=dark
colorscheme solarized
highlight Cursor guibg=#eabf50

if has('mouse')
    set mouse=a
endif

" change default color of concealed contents
highlight clear Conceal
highlight default link Conceal Statement

